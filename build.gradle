plugins {
	id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.25"
	id 'idea'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'jacoco'
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
}

group = 'bdd.demo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def versions = [
		cucumber: '7.8.1',
		openapi: '1.6.12'
]

repositories {
	mavenCentral()
}

sourceSets {
	// Create separate integration test source sets
	testInt {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

idea {
	module {
		testSourceDirs += project.sourceSets.testInt.java.srcDirs
		testSourceDirs += project.sourceSets.testInt.resources.srcDirs
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	// Need this to start @SpringBootTest
	testIntImplementation {
		extendsFrom implementation
	}

	// Need this to start @SpringBootTest
	testIntRuntimeOnly {
		extendsFrom runtimeOnly
	}

	// h2 server configuration
	h2 {
		extendsFrom runtimeOnly
	}
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation "org.springdoc:springdoc-openapi-ui:${versions.openapi}"

	runtimeOnly 'com.h2database:h2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Integration test dependencies
	testIntAnnotationProcessor 'org.projectlombok:lombok'
	testIntCompileOnly 'org.projectlombok:lombok'
	testIntImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Cucumber dependencies
	testIntImplementation platform("io.cucumber:cucumber-bom:${versions.cucumber}")
	testIntImplementation "io.cucumber:cucumber-java"
	testIntImplementation "io.cucumber:cucumber-junit-platform-engine"
	testIntImplementation "io.cucumber:cucumber-spring"

	// rest-assured version managed by spring boot
	testIntImplementation "io.rest-assured:rest-assured"

	// Need junit-platform-suite to run cucumber tests. see RunCucumberTest
	testIntImplementation "org.junit.platform:junit-platform-suite"
}

// Use ant task to start the database so the execution is not blocked
tasks.register('startDB') {
	description = 'Test classpath'
	group = 'application'
	doLast {
		println "Classpath: ${configurations.h2.asPath}"
		ant.java(classname: 'org.h2.tools.Server', classpath: configurations.h2.asPath, fork: false, spawn: false, outputproperty: 'op') {
			arg(value: '-tcp')
			arg(value: '-baseDir')
			arg(value: "${project.projectDir}/db")
			// arg(value: '-ifExists') // Connect to existing databases only. Database will not be created if it does not exist
			arg(value: '-ifNotExists') // Allow database creation
			arg(value: '-web')
			arg(value: '-tcpPassword')
			arg(value: 'sa')
		}
		println ant.properties.op
	}
}

task stopDB(type: JavaExec) {
	description = 'Stop H2 databse server'
	group = 'application'
	classpath = configurations.h2
	mainClass = 'org.h2.tools.Server'
	args = ['-tcpShutdown', 'tcp://localhost', '-tcpPassword', 'sa']
}

// Run integration test
task testInt(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'
	// Always run integration test
	outputs.upToDateWhen { false }
	testClassesDirs = sourceSets.testInt.output.classesDirs
	classpath = sourceSets.testInt.runtimeClasspath
	// Run integration test after running the unit tests
	shouldRunAfter test
}

check.configure {
	// check task depends on testInt and jacocoTestCoverageVerification
	// when you run ./gradlew check testInt and jacocoTestCoverageVerification tasks are also run
	dependsOn testInt
	dependsOn jacocoTestCoverageVerification
}

cucumberReports {
	buildId = '0'
	outputDir = file("${buildDir}/reports/cucumber")
	reports = files("${buildDir}/reports/cucumber/cucumber-report.json")
}

jacocoTestCoverageVerification {
	dependsOn testInt
	executionData {
		tasks.withType(Test).findAll {
			it.jacoco.destinationFile.exists()
		}*.jacoco.destinationFile
	}
	violationRules {
		rule {
			element = 'CLASS'
			excludes = [
					'bdd.demo.ecommerce.EcommerceApplication',	         // ignore the main application
					'bdd.demo.ecommerce.configuration.OpenApiConfig',	 // ignore api specification generation class
					'bdd.*.Constants'	                                 // ignore all the constant files
			]
			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.95
			}
		}
	}
}

jacocoTestReport {
	// https://stackoverflow.com/questions/19025138/gradle-how-to-generate-coverage-report-for-integration-test-using-jacoco
	executionData {
		tasks.withType(Test).findAll {
			it.jacoco.destinationFile.exists()
		}*.jacoco.destinationFile
	}

	reports {
		xml.required = true
		// HTML report: build/reports/jacoco/test/html/index.html
		html.required = true
	}
}

tasks.withType(Test) {
	useJUnitPlatform()

	// Need the following to show the output when running from the console
	testLogging {
		events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
	}
	testLogging.showStandardStreams = false
	finalizedBy jacocoTestReport // report is always generated after tests run
}
